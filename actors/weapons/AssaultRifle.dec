Actor AssaultRifleAmmo : Ammo
{
	Game Doom
	Inventory.Amount 30
	Inventory.MaxAmount 30
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 30
}

Actor TangoAssaultRifle : Weapon 28705 //replaces Chaingun
{
	//$Category Weapons
	//$Title "TangoAssaultRifle"
	Tag "Assault Rifle"
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	decal "bulletchip"
	Weapon.SlotNumber 4
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 0
	Weapon.AmmoType2 "Clip"
	Weapon.AmmoType1 "AssaultRifleAmmo"
	Inventory.PickupSound "items/assaultrifle"
	Obituary "%o was gunned down by %k's Assault Rifle"
	Inventory.PickupMessage "$TANGO_ASSAULT_RIFLE"
	Weapon.Kickback 50
	Weapon.AmmoGive2 20
	
	States
	{
	Spawn:
		TARP A -1
		Stop
	Ready:
		TARG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
	    TNT1 A 0 A_JumpIfNoAmmo("Reload")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("weapons/arfire", CHAN_WEAPON)
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_Light1
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_screen_shake") == 0, "SkipShake1")
		TNT1 A 0 Radius_Quake(2, 2, 0, 1, 0)
		SkipShake1:
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil1")
		TNT1 A 0 A_SetPitch(pitch - 0.4)
		SkipRecoil1:
		
		TARG B 1 Bright A_FireBullets(3, 3, -1, 20, "BulletPuff", FBF_USEAMMO|FBF_NORANDOM, 8192) // , "BulletTracer", 0)
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil2")
		TNT1 A 0 A_SetPitch(pitch + 0.2)
		SkipRecoil2:
		
		TNT1 A 0 A_Light0
		TARG C 1 Bright 
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil3")
		TNT1 A 0 A_SetPitch(pitch + 0.2)
		SkipRecoil3:
		
		TNT1 A 0 A_SpawnItemEx("ChaingunCasing", 30 * cos(pitch), 0, 30 - (30 * sin(pitch)), frandom(2, 4) * cos(pitch), frandom(3, 6), frandom(3, 6) * -sin(pitch), 0, SXF_NOCHECKPOSITION | SXF_TRANSFERPITCH)
		TARG DE 1
		TARG FG 1 A_Refire
		TNT1 A 0 A_JumpIfNoAmmo("CheckAutoReload")
		Goto Ready
	CheckAutoReload:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_auto_reload") == 1, "ReloadPreBuffer")
		Goto Ready
	ReloadPreBuffer:
		TARG A 1 A_WeaponReady(WRF_NOFIRE)
		Goto Reload
	NoAmmo:
		TARG A 2 A_PlaySound("weapons/empty")
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("Clip", 1, 1)
		Goto NoAmmo
		TNT1 A 0 A_JumpIfInventory("AssaultRifleAmmo", 0, "Ready")
		
		TARR ABCC 1
		TARR DF 1
		TNT1 A 0 A_PlaySound("weapons/arright", CHAN_AUTO)
		TARR GIJ 1
		TARR KLMPON 1
		TRR1 ABCDE 1
		TRR1 F 3
		TRR1 GH 1
		TNT1 A 0 A_PlaySound("weapons/aropen", CHAN_AUTO)
		TRR1 IJ 1
		TNT1 A 0 A_SpawnItemEx("ClipCasing", 22, 0, 38, frandom(0, 1.0), frandom(-6.5, -8.0), frandom(1.0, 3.5)) 
		TRR1 KLM 1
		TRR1 N 2
	ReloadRepeat:
		TNT1 A 0 A_JumpIfInventory("AssaultRifleAmmo", 0, "ReloadFinish")
		TNT1 A 0 A_JumpIfInventory("Clip", 1, 1)
		Goto ReloadFinish
		TNT1 A 0 A_GiveInventory("AssaultRifleAmmo", 1)
		TNT1 A 0 A_Takeinventory("Clip",1)
		Goto ReloadRepeat
	ReloadFinish:
		TRR2 AB 2
		TRR2 C 1
		TNT1 A 0 A_PlaySound("weapons/arin", CHAN_AUTO)
		TRR2 DE 1
		TRR2 F 3
		TRR2 G 5
		TRR2 HIJ 1
		TARR NK 1
		TNT1 A 0 A_PlaySound("weapons/arleft", CHAN_AUTO)
		TARR HFDCBA 1
		Goto Ready
	Flash:
		TNT1 A 0
		Goto LightDone
	Select:
		TNT1 A 0 A_PlaySound("items/assaultrifle", CHAN_WEAPON)
		TARG A 0 A_Raise
		Loop
	Deselect:
		TARG A 0 A_Lower
		Loop
	}
}