// Pretty sure this is unused...
ACTOR SuperShotgunAmmo : Ammo
{
	Game Doom
	Inventory.Amount 2
	Inventory.MaxAmount 2
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 2
}

ACTOR TangoSuperShotgun : Weapon replaces SuperShotgun
{
	Tag "Super Shotgun"
	+WEAPON.AMMO_OPTIONAL
	+INVENTORY.UNDROPPABLE
	+WEAPON.NOALERT
	Weapon.SelectionOrder 400
	Weapon.SlotNumber 3
	decal bulletchip
	Weapon.KickBack 150
	Weapon.AmmoUse1 2
	Weapon.AmmoGive1 8
	Weapon.AmmoType1 "TangoShell"
	Inventory.PickupMessage "$TANGO_SUPER_SHOTGUN"
	Inventory.PickupSound "weapons/sshotl"
	States
	{
	Spawn:
		SSGP A -1
		Stop
	Ready:
		SHTE A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		SHTE A 0 A_Lower
		Loop
	Select:
		TNT1 A 0 A_PlaySound("weapons/sshotl", CHAN_WEAPON)
		SHTE A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_JumpIfInventory("TangoShell",2,1)
        Goto NoAmmo
		
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_Light1
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_screen_shake") == 0, "SkipShake")
		TNT1 A 0 Radius_Quake(8, 3, 0, 1, 0)
		SkipShake:
		TNT1 A 0 A_AlertMonsters
		
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil1")
		TNT1 A 0 A_SetPitch(pitch - 1.2)
		SkipRecoil1:
		
		// We use shotgunpuff so that the bullets have the Shotgun damage type
		TNT1 A 0 A_FireBullets (7.1, 7.1, 21, 10, "ShotgunPuff", FBF_USEAMMO|FBF_NORANDOM, 8192) // , "BulletTracer", 0)

		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_classic_ssg_fire_sound") == 1, "PlayClassicSound")
		TNT1 A 0 A_PlaySound("ssg/fire", CHAN_WEAPON)
		Goto PostSoundFire
		
		PlayClassicSound:
		TNT1 A 0 A_PlaySound("ssg/fireclassic", CHAN_WEAPON)
		Goto PostSoundFire
		
		PostSoundFire:
		SH2F A 2 Bright A_GunFlash
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil2")
		TNT1 A 0 A_SetPitch(pitch + 0.6)
		SkipRecoil2:
		SH2F B 1 Bright A_Light2
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("tango_cvar_enable_recoil") == 0, "SkipRecoil3")
		TNT1 A 0 A_SetPitch(pitch + 0.6)
		SkipRecoil3:
		SH2F B 1 Bright
		SHTE A 1 A_Light0
		SH2E A 1
		TNT1 A 0 A_JumpIfInventory("TangoShell",2,1)
		Goto NoAmmo
		SHT2 B 1
		SHT2 CD 1
		SHT2 F 3
		SHT2 EDCB 1
		// SHT2 RQ 2
		SH2E B 2
		SHT2 GH 1
		SH2E F 1 A_PlaySoundEx ("weapons/sshoto", CHAN_AUTO)
		SHT2 I 1
		TNT1 A 0 A_SpawnItemEx("ShotgunCasing",random(-4,-5),cos(pitch)*25,sin(-pitch)*25+random(20,22), random(2,3),0,random(5,7),	random(90,105))
		TNT1 A 0 A_SpawnItemEx("ShotgunCasing",random(-6,-7),cos(pitch)*25,sin(-pitch)*25+random(20,22), random(2,3),0,random(5,7),	random(100,110))	
		SH2E D 4
		SHT2 J 2
		SH2E EH 2
		SHT2 K 2
		SHT2 L 3 A_PlaySoundEx ("weapons/sshotl", CHAN_AUTO)
		SHT2 M 2
		SHT2 N 2
		SHT2 OP 1
		SH2E I 1 A_PlaySoundEx ("weapons/sshotc", CHAN_AUTO)
		SH2E C 1
		SH2E B 2
		SHT2 QR 2
		Goto Ready
	NoAmmo:
		SHTE A 2 A_PlaySound("weapons/empty")
		Goto Ready
	}
}